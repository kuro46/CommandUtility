plugins {
    id 'net.ltgt.errorprone' version '1.1.1'
    id 'io.freefair.lombok' version '4.1.6'
    id 'maven-publish'
    id 'java'
}

group = 'xyz.shirokuro'
version = '0.6.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
}

dependencies {
    compileOnly 'org.bukkit:bukkit:1.12.2-R0.1-SNAPSHOT'
    testImplementation 'org.bukkit:bukkit:1.12.2-R0.1-SNAPSHOT'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.2'
    // ErrorProne
    errorprone 'com.google.errorprone:error_prone_core:2.+'
}

if (!JavaVersion.current().isJava9Compatible()) {
    dependencies {
        errorproneJavac 'com.google.errorprone:javac:9+181-r4173-1'
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
}

tasks.test {
    useJUnitPlatform()
    testLogging {
        events('passed', 'skipped', 'failed')
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

// maven

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = rootProject.name
            from components.java
            pom {
                name = 'CommandUtility'
                url = 'https://github.com/kuro46/CommandUtility'
            }
        }
    }
    repositories {
        maven {
            def publishRootDir = project.hasProperty("commandutility.publish.rootdir")
                ? project.getProperty("commandutility.publish.rootdir").toString()
                : buildDir.toString()
            def releasesRepoUrl = "$publishRootDir/repos/releases"
            def snapshotsRepoUrl = "$publishRootDir/repos/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
